// Based on the Lezer JavaScript grammer
// <https://github.com/lezer-parser/javascript/blob/main/src/javascript.grammar>

@skip { spaces | LineComment }

@precedence {
  member,
  access,
  seq @right,
  call,
  prefix,
  postfix,
  exp @right,
  times @left,
  plus @left,
  updateRule @right,
  rel @left
}

@top Program { statement* }

SimpleStatement {
  expressionMaybeSeq StyleMapping?
}

statement[@isGroup=Statement] {
  SimpleStatement |
  RegressionStatement { expression "~" expression RegressionBody? StyleMapping? } |
  Table { kw<"table"> BlockInner StyleMapping? } |
  Image { kw<"image"> String String StyleMapping? } |
  Text { String StyleMapping? } |
  Folder { kw<"folder"> String BlockInner StyleMapping? } |
  Settings { kw<"settings"> StyleMapping }
}

RegressionBody {
  "#" Identifier "{" statement* "}"
}

BlockInner {
  "{" statement* "}"
}

expressionMaybeSeq[@isGroup=Expression] {
  expression |
  SequenceExpression { expression !seq "," expressionMaybeSeq }   
}

expression {
  Number |
  Identifier |
  RepeatedExpression |
  ListExpression |
  ListComprehension { "[" expression kw<"for"> commaSep1<AssignmentExpression> "]" } |
  Piecewise { "{" commaSep<PiecewiseBranch> "}" } |
  UpdateRule { expression !updateRule "->" expression } |
  PrefixExpression { !prefix ArithOp<"-"> expression } |
  ParenthesizedExpression { "(" expressionMaybeSeq ")" } |
  MemberExpression { expression !member DotAccessIdentifier } |
  ListAccessExpression { expression !access ListExpression } |
  BinaryExpression |
  PostfixExpression { expression !postfix ArithOp<"!"> } |
  CallExpression
}

PiecewiseBranch { expression (":" expression)? }

AssignmentExpression { Identifier "=" expression }

CallExpression { expression !call "(" commaSep<expression> ")" }

StyleMapping {
  MappingStart commaSep<MappingEntry> MappingEnd
}

MappingEntry { Identifier ":" mappingValue }

mappingValue {
  expression |
  String |
  StyleMapping
}

RepeatedExpression {
  (kw<"integral"> | kw<"sum"> | kw<"product">)
  Identifier "=" "(" expression "..." expression ")"
  expression
}

ListExpression {
  "[" commaSep<expression> ("..." ","? commaSep<expression>)? "]"
}

BinaryExpression {
  expression !exp ArithOp<"^"> expression |
  expression !times (divide | ArithOp<"*">) expression |
  expression !plus ArithOp<"+" | "-"> expression |
  expression !rel CompareOp<"<" "="? | ">" "="? | "="> expression
}

commaSep<content> {
  "" | content ("," content)* ","?
}

commaSep1<content> {
  content ("," content)* ","?
}

ArithOp<expr> { expr }
CompareOp<expr> { expr }

// Keywords
kw<term> { @specialize[@name={term}]<Identifier, term> }

@tokens {
  Identifier { $[a-zA-Z] $[a-zA-Z0-9]* }

  // Needed to avoid confusion with p .5x being dot access: p.5x
  DotAccessIdentifier { "." $[a-zA-Z] $[a-zA-Z0-9]* }

  String { '"' (!["\\] | "\\" _)* '"' }

  Number {
    std.digit+ ("." (std.digit)+)? |
    "." std.digit+
  }

  LineComment { "//" ![\n]* }

  divide[@name=ArithOp] { "/" }

  @precedence { LineComment, divide }

  @precedence { "->", "-" }

  spaces { $[ \t\n\r]+ }

  MappingStart[closedBy=MappingEnd] { "@{" }
  MappingEnd[openedBy=MappingStart] { "}" }


  "(" ")" "[" "]" "{" "}"

  "..." "," "=" ":" "->"
}

@detectDelim
