// Based on the Lezer JavaScript grammer
// <https://github.com/lezer-parser/javascript/blob/main/src/javascript.grammar>

@skip { spaces | LineComment }

@precedence {
  member,
  access,
  call,
  prefix,
  postfix,
  typeof,
  exp @right,
  times @left,
  plus @left,
  shift @left,
  loop,
  rel @left
}


@top Program { line* }

line[@isGroup=Statement] {
  ShowStatement { ShowOrHidden expression style? } |
  LetStatement { kw<"let"> (Identifier | CallExpression) "=" expression style? } |
  RegressionStatement { kw<"regression"> expression "~" expression style? } |
  Table |
  Image { kw<"image"> String String style? } |
  Text { String } |
  Folder { kw<"folder"> String FolderInner style? } |
  Settings { kw<"settings"> style }
}

FolderInner {
  "{" line* "}"
}

Table {
  kw<"table"> TableInner style?
}

TableInner {
  "{"
    (Identifier "=" ListExpression)?
    tableColumn+
  "}"
}

tableColumn {
  ShowOrHidden
  tableColumnInner {
    ColumnAssignment { Identifier "=" ListExpression style? } |
    ColumnCalc { expression }
  }
}

ShowOrHidden { kw<"show"> | kw<"calc"> }

expression {
  Number |
  Identifier |
  String |
  RepeatedExpression |
  ListExpression |
  ListComprehension { "[" expression kw<"for"> commaSep1<Identifier "=" expression> "]" } |
  Piecewise { "{" commaSep<expression ":" expression> "}" } |
  PrefixExpression { !prefix ArithOp<"-"> expression } |
  ParenthesizedExpression { "(" expression ")" } |
  MemberExpression { expression !member "." Identifier } |
  ListAccessExpression { expression !access ListExpression }
  BinaryExpression |
  PostfixExpression { expression !postfix ArithOp<"!"> } |
  CallExpression
}

CallExpression { expression !call "(" commaSep<expression> ")" }

Mapping {
  "@" "{" commaSep<Identifier ":" (expression | style) > "}" 
}

style {
  Mapping
}

RepeatedExpression {
  (kw<"integral"> | kw<"sum"> | kw<"product">)
  Identifier "=" "(" expression Ellipsis expression ")"
  expression
}

ListExpression { 
  // "[" commaSep<expression> (","? "..." ","? commaSep<expression>)? "]"
  "[" commaSep<expression> (Ellipsis commaSep<expression>)? "]"
}

BinaryExpression {
  expression !exp ArithOp<"^"> expression |
  expression !times (divide  | ArithOp<"%"> | ArithOp<"*">) expression |
  expression !plus ArithOp<"+" | "-"> expression |
  expression !rel CompareOp<"<" "="? | ">" "="? | "=="> expression
}

commaSep<content> {
  "" | content ("," content)*
}

commaSep1<content> {
  content ("," content)*
}

ArithOp<expr> { expr }
CompareOp<expr> { expr }

// Keywords
kw<term> { @specialize[@name={term}]<Identifier, term> }

@tokens {
  Identifier { $[a-zA-Z] $[a-zA-Z0-9]+ }

  String { '"' (!["\\] | "\\" _)* '"' }

  Number {
    std.digit+ ("." (std.digit)*)? |
    "." std.digit+
  }

  LineComment { "//" ![\n]* }

  divide[@name=ArithOp] { "/" }

  @precedence { LineComment, divide }

  Ellipsis { "..." }

  @precedence { Number, Ellipsis }

  spaces { $[ \t\n\r]+ }


  "(" ")" "[" "]" "{" "}"

  "," "=" ":"
}

@detectDelim
